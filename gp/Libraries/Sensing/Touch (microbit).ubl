module 'Touch (microbit)' Input
author MicroBlocks
version 1 2 
description 'Capacitive touch sensing for micro:bit, Calliope mini, and other boards with touch pins. Works with pins that have 1M ohm external pullup resistors.
'
variables _lowCount _isHigh 

  spec 'r' 'touch' 'pin _ touched : threshold _' 'num num' 0 0
  spec 'r' 'logo touched' 'logo touched'

to 'logo touched' {
  if ((boardType) == 'micro:bit') {return (booleanConstant false)}
  return (touch 26 0)
}

to touch pin optionalThreshold {
  local 'threshold' (argOrDefault 2 15)
  comment 'Discharge pin'
  digitalWriteOp pin false
  waitMillis 5
  local 'n' 0
  comment 'Wait for pin to become high, incrementing n'
  repeatUntil (digitalReadOp pin) {
    n += 1
    if (n >= threshold) {
      comment 'Long charging time means pin is touched'
      _isHigh = (booleanConstant true)
      _lowCount = 0
      digitalWriteOp pin false
      return _isHigh
    }
  }
  digitalWriteOp pin false
  if (_isHigh == 0) {
    _isHigh = (booleanConstant false)
  }
  comment 'count is < threshold so pin not touched'
  if _isHigh {
    comment 'Require several low counts in a row
to exit "isHigh" state to avoid jitter.'
    _lowCount += 1
    if (_lowCount > 3) {
      _isHigh = (booleanConstant false)
    }
  }
  return _isHigh
}

