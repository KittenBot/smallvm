module Rhythm Control
author MicroBlocks
version 2 2 
description 'Keeps time according to a tempo and time signature. Useful to create musical patterns.

Includes abstractions for note durations, bars and beats.
'
variables _rhy_tempo _rhy_bars _rhy_looping _rhy_beat_duration _rhy_measures 

  spec ' ' 'rhy_set tempo' 'set tempo to _ bpm at _ / _ signature' 'auto auto auto' 250 4 4
  space
  spec ' ' 'rhy_start counting' 'start counting _ bars' 'auto' 16
  spec ' ' 'rhy_stop counting' 'stop counting'
  space
  spec 'r' 'rhy_current bar' 'current bar'
  spec 'r' 'rhy_current beat' 'current beat'
  space
  spec ' ' 'rhy_wait until next' 'wait until next _ duration' 'auto' ''
  spec ' ' 'rhy wait until beat' 'wait until beat _' 'auto' 3
  spec ' ' 'rhy wait until bar' 'wait until bar _' 'auto' 2
  space
  spec 'r' 'rhy_whole duration' '¹⁄₁'
  spec 'r' 'rhy_half duration' '½'
  spec 'r' 'rhy_quarter duration' '¼'
  spec 'r' 'rhy_eighth duration' '⅛'
  spec 'r' 'rhy_sixteenth duration' '¹⁄₁₆'
  space
  spec 'r' 'rhy_dotted' '_ .' 'auto' ''
  spec 'r' 'rhy_triplet' '‿³ _' 'auto' ''
  space
  spec ' ' '_rhy loop' '_rhy loop'
  spec 'r' '_rhy duration of note value' '_rhy duration of note value _' 'auto' 4
  spec 'r' '_rhy 2 ^' '_rhy 2 ^ _' 'auto' '10'
  spec 'r' '_rhy between' '_rhy _ between _ and _' 'auto auto auto' '10' '10' '10'

to '_rhy 2 ^' n {
  local 'result' 1
  repeat n {
    result = (result * 2)
  }
  return result
}

to '_rhy between' foo a b {
  return (and (foo >= a) (foo <= b))
}

to '_rhy duration of note value' 'note value' {
  return ((_rhy_beat_duration * _rhy_reference_note) / (v 'note value'))
}

to '_rhy loop' {
  repeatUntil (not _rhy_looping) {
    for bar _rhy_bars {
      for beat _rhy_measures {
        if (not _rhy_looping) {stopTask}
        'rhy_wait until next' _rhy_beat_duration
      }
    }
  }
}

to 'rhy wait until bar' 'bar number' {
  local 'bar duration' (_rhy_measures * _rhy_beat_duration)
  local 'round duration' ((v 'bar duration') * _rhy_bars)
  local 'wakeTime' (((v 'round duration') * (((millisOp) / (v 'round duration')) + 1)) + (((v 'bar number') - 1) * (v 'bar duration')))
  waitMillis ((wakeTime - (millisOp)) % (v 'round duration'))
}

to 'rhy wait until beat' 'beat number' {
  local 'bar duration' (_rhy_measures * _rhy_beat_duration)
  local 'wakeTime' (((v 'bar duration') * (((millisOp) / (v 'bar duration')) + 1)) + (((v 'beat number') - 1) * _rhy_beat_duration))
  waitMillis ((wakeTime - (millisOp)) % (v 'bar duration'))
}

to 'rhy_current bar' {
  local 'bar duration' (_rhy_measures * _rhy_beat_duration)
  local 'round duration' ((v 'bar duration') * _rhy_bars)
  return ((((millisOp) % (v 'round duration')) / (v 'bar duration')) + 1)
}

to 'rhy_current beat' {
  local 'bar duration' (_rhy_measures * _rhy_beat_duration)
  return ((((millisOp) % (v 'bar duration')) / _rhy_beat_duration) + 1)
}

to rhy_dotted duration {
  return (duration + (duration / 2))
}

to 'rhy_eighth duration' {
  return ('_rhy duration of note value' 8)
}

to 'rhy_half duration' {
  return ('_rhy duration of note value' 2)
}

to 'rhy_quarter duration' {
  return ('_rhy duration of note value' 4)
}

to 'rhy_set tempo' tempo measures 'note value' {
  _rhy_tempo = tempo
  _rhy_reference_note = (v 'note value')
  _rhy_beat_duration = (60000 / _rhy_tempo)
  _rhy_measures = measures
}

to 'rhy_sixteenth duration' {
  return ('_rhy duration of note value' 16)
}

to 'rhy_start counting' bars {
  if (_rhy_measures == 0) {'rhy_set tempo' 240 4 4}
  _rhy_bars = bars
  _rhy_looping = (booleanConstant true)
  sendBroadcast '_rhy loop'
}

to 'rhy_stop counting' {
  _rhy_looping = (booleanConstant false)
}

to rhy_triplet duration {
  return ((duration * 2) / 3)
}

to 'rhy_wait until next' duration {
  local 'wakeTime' (duration * (((millisOp) / duration) + 1))
  waitMillis (wakeTime - (millisOp))
}

to 'rhy_whole duration' {
  return ('_rhy duration of note value' 1)
}

