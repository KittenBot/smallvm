module NeZha Output
author wwj718
version 1 2 
choices servoType '180' '270' '360' 
choices RJ_pin J1 J2 J3 J4 
choices inputPin J1 J2 
description 'Nezha is an expanstion board for micro:bit with multuple functions available. It equips with four servos connections, four motors connections and seven sensors connections. All of the connections are required with RJ11 connectors that are fool-proofing and easy to plug. It is also compatible to Lego and Fischertechnik bricks and is available to get them connected with the electric modules, thus it allows students to easily create their own amazing micro:bit projects.

https://www.elecfreaks.com/learn-en/microbitExtensionModule/nezha.html'
variables _NEZHA_ADDR '_NeZha initialized' 

  spec ' ' 'set motor speed' 'NeZha set motor _ speed to _ %' 'menu.range:1-4 num' 1 100
  spec ' ' 'stop motor' 'NeZha stop motor _' 'menu.range:1-4' 1
  spec ' ' 'stop all motors' 'NeZha stop all motors'
  spec ' ' 'set servo angle' 'NeZha set _ servo _ angle to _' 'menu.servoType menu.range:1-4 num' '360' 1 0
  spec ' ' 'set continuous rotation servo speed' 'NeZha set continuous rotation servo _ speed to _ %' 'menu.range:1-4 num' 1 0
  spec ' ' '_NeZha setup' 'NeZha _NeZha setup'

to '_NeZha setup' {
  if ((v '_NeZha initialized') == 0) {
    _NEZHA_ADDR = (hexToInt '10')
    '_NeZha initialized' = 1
  }
}

to 'set continuous rotation servo speed' servo angle {
  comment 'motor (number): 1 2 3 4'
  comment 'speed (number): -100-100'
  if (or (angle < -100) (angle > 100)) {
    angle = 0
  }
  'set servo angle' '180' servo ('[misc:rescale]' angle -100 100 0 180)
}

to 'set motor speed' motor speed {
  '_NeZha setup'
  comment 'motor (number): 1 2 3 4'
  comment 'speed (number): -100-100'
  if (or (motor < 1) (motor > 4)) {
    motor = 1
  }
  if (or (speed < -100) (speed > 100)) {
    speed = 0
  }
  if (speed < 0) {
    '[sensors:i2cWrite]' _NEZHA_ADDR ('[data:asByteArray]' ('[data:makeList]' motor 2 (speed * -1) 0))
  } else {
    '[sensors:i2cWrite]' _NEZHA_ADDR ('[data:asByteArray]' ('[data:makeList]' motor 1 speed 0))
  }
}

to 'set servo angle' servo_type servo angle {
  '_NeZha setup'
  comment 'servo (number): 1 2 3 4'
  comment '''180'' ''270'' ''360''

str -> num'
  servo_type = (absoluteValue servo_type)
  if (or (servo < 1) (servo > 4)) {
    servo = 1
  }
  if (or (angle < 0) (angle > servo_type)) {
    angle = 0
  }
  '[sensors:i2cWrite]' _NEZHA_ADDR ('[data:asByteArray]' ('[data:makeList]' ((hexToInt '10') + (servo - 1)) ('[misc:rescale]' angle 0 servo_type 0 180) 0 0))
}

to 'stop all motors' {
  '_NeZha setup'
  for i 4 {
    'set motor speed' i 0
  }
}

to 'stop motor' index {
  '_NeZha setup'
  'set motor speed' index 0
}

